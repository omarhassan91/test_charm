* Tests domdec's solvent-solvent, solvent-solute and solute-solute routines.
* We make a dummy water model that's a renamed TIP3P to trigger the solute
* routines, checking that results are consistent between solute and solvent.
* Both double precision and single precision are tested; finite difference checks
* are performed for the former, but not the latter due to insufficient precision.
*

if ?ljpme .ne. 1 then
 echo "Test NOT performed."
 stop
endif

if ?numnode .gt. 1 then stop ! This tiny toy system causes problems in domdec

format (F20.10)

stream datadir.def

set dofindif 1
goto readrtfprm
label donertfprm

set refenergy 134.67496
faster off
!
! Regular CHARMM
!
set testname charmmUV
goto readcoorUV
label start_charmmUV
set enginetype
set precision
goto runtest
label end_charmmUV

!
! DOMDEC DOUBLE PRECISION TESTS
!
faster on

set testname domdec_double_VV
goto readcoorVV
label start_domdec_double_VV
set enginetype domdec
set precision
goto runtest
label end_domdec_double_VV

set testname domdec_double_UV
goto readcoorUV
label start_domdec_double_UV
set enginetype domdec
set precision
goto runtest
label end_domdec_double_UV

set testname domdec_double_UU
goto readcoorUU
label start_domdec_double_UU
set enginetype domdec
set precision
goto runtest
label end_domdec_double_UU

!
! DOMDEC SINGLE PRECISION TESTS
!
set dofindif 0

set testname domdec_single_VV
goto readcoorVV
label start_domdec_single_VV
set enginetype domdec
set precision single
goto runtest
label end_domdec_single_VV

set testname domdec_single_UV
goto readcoorUV
label start_domdec_single_UV
set enginetype domdec
set precision single
goto runtest
label end_domdec_single_UV

set testname domdec_single_UU
goto readcoorUU
label start_domdec_single_UU
set enginetype domdec
set precision single
goto runtest
label end_domdec_single_UU

stop

!==============================================================
label runtest
crystal define cubic 20 20 20 90 90 90
crystal build cutoff 30 noper 0
image byseg xcen 0.0 ycen 0.0 zcen 0.0 select segid 5dfr end
image byres xcen 0.0 ycen 0.0 zcen 0.0 select segid wat end

energy cutnb 10 cutim 14 ctofnb 8 ctonnb 8 -
     ljpme dkappa 0.4 -
     @enginetype @precision -
     ewald pmew kappa 0.3 fftx 48 ffty 48 fftz 48 order 5


set lastenergy = ?ENER
@testcheck ?ENER @refenergy 0.0001 @testname_firstenergy

if @dofindif .gt. 0 then
  ! Finite difference tests
  calc ndf = 3*?natom
  test first tol 0.0000001 step 0.000001 select all end
  @testcheck ?NOK @ndf 0 @testname_testfirst
endif

goto end_@testname

!==============================================================
label readrtfprm


read rtf card
* Topology for water and ions
*
31  1
MASS  -1   OT   15.99940 O  ! TIPS3P WATER OXYGEN
MASS  -1   HT    1.00800 H  ! TIPS3P WATER HYDROGEN

auto angles dihe

RESI TIP3         0.000 ! tip3p water model, generate using noangle nodihedral
GROUP
ATOM O    OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417

BOND O H1 O H2 H1 H2    ! the last bond is needed for shake
ANGLE H1 O H2             ! required

DONOR H1 O
DONOR H2 O
ACCEPTOR O
PATCHING FIRS NONE LAST NONE

RESI H2O          0.000 ! tip3p water model, generate using noangle nodihedral
GROUP
ATOM O    OT     -0.834
ATOM H1   HT      0.417
ATOM H2   HT      0.417

BOND O H1 O H2 H1 H2    ! the last bond is needed for shake
ANGLE H1 O H2             ! required

DONOR H1 O
DONOR H2 O
ACCEPTOR O
PATCHING FIRS NONE LAST NONE
END


read para card flex
* tip3p prm file
*

ATOMS

MASS  -1   OT   15.99940  ! TIPS3P WATER OXYGEN
MASS  -1   HT    1.00800  ! TIPS3P WATER HYDROGEN

BONDS
!
!V(bond) = Kb(b - b0)**2
!
!Kb: kcal/mole/A**2
!b0: A
!
!atom type Kb          b0
!
HT    HT      0.0       1.5139  ! from TIPS3P geometry (for SHAKE w/PARAM)
HT    OT    450.0       0.9572  ! from TIPS3P geometry

ANGLES
!
!V(angle) = Ktheta(Theta - Theta0)**2
!
!V(Urey-Bradley) = Kub(S - S0)**2
!
!Ktheta: kcal/mole/rad**2
!Theta0: degrees
!Kub: kcal/mole/A**2 (Urey-Bradley)
!S0: A
!
!atom types     Ktheta    Theta0   Kub     S0
!
HT   OT   HT     55.0      104.52   ! FROM TIPS3P GEOMETRY

NONBONDED nbxmod 3 atom cdiel switch vswitch vatom -
cutnb 14.0 ctofnb 12.0 ctonnb 10.0 eps 1.0 e14fac 1.0

!TIP3P LJ parameters
OT       0.0       -0.1521    1.7682
HT       0.0       -0.046     0.2245

HBOND CUTHB 0.5

END

goto donertfprm

!========================================================================

label readcoorVV

delete atoms select all end

READ      SEQUENCE  CARD
* Water dimer
*
    6
TIP3 TIP3

GENERATE VV NOANGLE NODIHEDRAL

READ COOR CARDS
* autogenerated from water.xyz
*
         6  EXT
         1         1  TIP3      O               2.000000            2.000000            2.000000      VV        1               0.0
         2         1  TIP3      H1              2.500000            2.000000            3.000000      VV        2               0.0
         3         1  TIP3      H2              1.500000            2.000000            3.000000      VV        0               0.0
         4         2  TIP3      O               0.000000            0.000000            0.000000      VV        1               0.0
         5         2  TIP3      H1              0.500000            0.000000            1.000000      VV        2               0.0
         6         2  TIP3      H2             -0.500000            0.000000            1.000000      VV        0               0.0

goto start_@testname

!========================================================================

label readcoorUV

delete atoms select all end

READ      SEQUENCE  CARD
* Water dimer
*
    6
H2O TIP3

GENERATE UV NOANGLE NODIHEDRAL

READ COOR CARDS
* autogenerated from water.xyz
*
         6  EXT
         1         1  H2O       O               2.000000            2.000000            2.000000      UV        1               0.0
         2         1  H2O       H1              2.500000            2.000000            3.000000      UV        2               0.0
         3         1  H2O       H2              1.500000            2.000000            3.000000      UV        0               0.0
         4         2  TIP3      O               0.000000            0.000000            0.000000      UV        1               0.0
         5         2  TIP3      H1              0.500000            0.000000            1.000000      UV        2               0.0
         6         2  TIP3      H2             -0.500000            0.000000            1.000000      UV        0               0.0

goto start_@testname

!========================================================================

label readcoorUU

delete atoms select all end

READ      SEQUENCE  CARD
* Water dimer
*
    6
H2O H2O

GENERATE UU NOANGLE NODIHEDRAL

READ COOR CARDS
* autogenerated from water.xyz
*
         6  EXT
         1         1  H2O       O               2.000000            2.000000            2.000000      UU        1               0.0
         2         1  H2O       H1              2.500000            2.000000            3.000000      UU        2               0.0
         3         1  H2O       H2              1.500000            2.000000            3.000000      UU        0               0.0
         4         2  H2O       O               0.000000            0.000000            0.000000      UU        1               0.0
         5         2  H2O       H1              0.500000            0.000000            1.000000      UU        2               0.0
         6         2  H2O       H2             -0.500000            0.000000            1.000000      UU        0               0.0

goto start_@testname
